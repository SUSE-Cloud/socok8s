#!groovy

// Global vars so they can be used in different steps
TestDocs = true
TestFunctional = true

pipeline {

    options {
        timestamps()
        parallelsAlwaysFailFast()
        // Note(jhesketh): Unfortunately we can't set a global timeout for the
        //                 pipeline as it would also apply to the post stages
        //                 and hence interrupt our cleanup.
    }

    agent {
        node {
            label "cloud-ccp-ci"
        }
    }

    parameters {
        /* first value in the list is the default */
        choice(choices: ['airship', 'osh'], description: 'Which deployment mechanism?', name: 'deployment')
        choice(choices: ['smoke', 'all'], description: 'Run smoke or all tempest tests?', name: 'tempest_test_type')
        choice(choices: ['ses_aio', 'ses_rook'], description: 'Which ses version to use?', name: 'ses_version')
        booleanParam(name: 'hold_instance_for_debug', defaultValue: false, description: 'Keep this instance up for 3 hours')
    }

    environment {
        /* env.BRANCH_NAME is available in multibranch plugin but not in pipeline plugin used in daily integration job */
        BRANCH_NAME_ONLY="${env.GIT_BRANCH.replaceAll("origin/", "")}"
        /* Sanitize ENVNAME (lowercase and remove some problematic characters)
           as the names of the heat stacks will be derived from this. Also
           the CaaSP Velum automation has issues with mixed case hostnames. */
        SOCOK8S_ENVNAME = "cloud-socok8s-${BRANCH_NAME_ONLY.replaceAll("[^a-zA-Z0-9-]+", "-").toLowerCase()}-${env.BUILD_NUMBER}"
        SOCOK8S_OPENSUSE_MIRROR="https://provo-mirror.opensuse.org"
        OS_CLOUD = "engcloud-socok8s-ci"
        SOCOK8S_TEST_CEPH_ROOK="true"
        KEYNAME = "engcloud-cloud-ci"
        DELETE_ANYWAY = "YES"
        DEPLOYMENT_MECHANISM = "openstack"
        ANSIBLE_VERBOSITY = 1
        ANSIBLE_STDOUT_CALLBACK = "yaml"
        USER = "jenkins" /* Why isn't this set in the jenkins environment? */
        USE_ARA = "True"
        ARA_DIR = "$WORKSPACE"
        SOCOK8S_DEPLOY_DSTAT = "YES"
        SOCOK8S_TEMPEST_SUBUNIT_OUTPUT = "True"
    }

    stages {
        stage('Show environment information') {
            steps {
                sh 'printenv'
                echo "Job parameters are: ${params}"
            }
        }
        stage('Set ssh-agent for caasp4 deployment') {
            when { expression { return  TestFunctional } }
            steps {
                sh "files/setup-ssh-agent-for-caasp4-ci.py ${env.SOCOK8S_ENVNAME}"
            }
        }
        stage('Check for updated files') {
            when { expression { env.BRANCH_NAME_ONLY != 'master' } }
            steps {
                script {
                    /* When doing PRs, make sure we don't test everything by default */
                    TestDocs = false
                    TestFunctional = false

                    /* Need to fetch master to check against it for the proper diff */
                    sh "git config --add remote.origin.fetch +refs/heads/master:refs/remotes/origin/master"
                    sh "git fetch --no-tags"
                    List<String> sourceChanged = sh(returnStdout: true, script: "git diff --name-only origin/master...origin/${env.BRANCH_NAME_ONLY}").split()
                    echo "Files changed for this PR:\n${sourceChanged.join('\n')}"
                    TestDocs = sourceChanged.any{it.contains("doc/")} or sourceChanged.any{it.contains("tox.ini")} or sourceChanged.any{it.contains("Jenkinsfile.integration")}
                    /* In the future we could add a conditional to auto TestFunctional if tox.ini or Jenkinsfile is changed */
                    /* when conditionals will be harder to grasp or different code paths */
                    TestFunctional = sourceChanged.any{(!it.contains("doc/") && !it.contains("research/"))}
                }
            }
        }

        stage('lint and docs') {
            parallel {
                stage('Lint Ansible playbooks') {
                    when { expression { return  TestFunctional } }
                    options {
                        timeout(time: 5, unit: 'MINUTES', activity: true)
                    }
                    steps {
                        sh "tox -e lint"
                    }
                }

                stage('Build docs') {
                    when { expression { return TestDocs } }
                    options {
                        timeout(time: 5, unit: 'MINUTES', activity: true)
                    }
                    steps {
                        sh "tox -e docs"
                    }
                    post {
                        success {
                            publishHTML target: [
                                allowMissing: false,
                                alwaysLinkToLastBuild: false,
                                keepAll: true,
                                reportDir: 'doc/build/html',
                                reportFiles: 'index.html',
                                reportName: 'Built Docs'
                            ]
                        }
                    }
                }
            }
        }
        stage('Clean tox dir') {
            when {
                anyOf {
                    expression { return  TestFunctional }
                    expression { return TestDocs }
                }
            }
            steps {
                sh "rm -Rf .tox"
            }
        }
        stage('Create network') {
            when { expression { return  TestFunctional } }
            options {
                timeout(time: 10, unit: 'MINUTES', activity: true)
            }
            steps {
                sh 'touch /tmp/${SOCOK8S_ENVNAME}.needcleanup'
                sh "./run.sh deploy_network"
            }
        }
        stage('Create VMs') {
            when { expression { return  TestFunctional } }
            options {
                timeout(time: 45, unit: 'MINUTES', activity: true)
            }
            parallel {
                stage('Deploy CaaSP4') {
                    steps {
                        sh "source ${WORKSPACE}/sock_${env.SOCOK8S_ENVNAME} && ./run.sh deploy_caasp"
                    }
                }
                stage('Deploy SES') {
                    when { expression  { params.ses_version == "ses_aio" } }
                    steps {
                        sh "./run.sh deploy_ses"
                    }
                }
                stage('Deploy CCP Deployer') {
                    steps {
                        sh "./run.sh deploy_ccp_deployer"
                        sh "./run.sh configure_ccp_deployer"
                    }
                }
            }
        }

        stage('Setup CaaSP workers for openstack') {
            when { expression { return  TestFunctional } }
            options {
                timeout(time: 10, unit: 'MINUTES', activity: true)
            }
            steps {
                sh "source ${WORKSPACE}/sock_${env.SOCOK8S_ENVNAME} && ./run.sh setup_caasp_workers_for_openstack"
            }
        }

        stage('Deploy SES on Kubernetes with rook') {
            when { expression  { params.ses_version == "ses_rook" } }
            steps {
                sh "./run.sh deploy_ses_rook"
            }
        }

        stage('Deploy OpenStack Helm') {
            options {
                timeout(time: 20, unit: 'MINUTES', activity: true)
            }
            when {
                allOf {
                    expression { params.deployment == "osh" }
                    expression { return  TestFunctional }
                }
            }
            steps {
                sh "./run.sh patch_upstream"
                sh "./run.sh build_images"
                sh "./run.sh deploy_osh"
            }
        }

        stage('Deploy Airship') {
            options {
                timeout(time: 45, unit: 'MINUTES', activity: true)
            }
            when {
                allOf {
                    expression { params.deployment == "airship" }
                    expression { return  TestFunctional }
                }
            }
            steps {
                sh "source ${WORKSPACE}/sock_${env.SOCOK8S_ENVNAME} && ./run.sh setup_airship"
            }
        }

        stage('Run Tempest') {
            options {
                timeout(time: 240, unit: 'MINUTES', activity: true)
            }
            when { expression { return  TestFunctional } }

            steps {
                sh """
                  export TEMPEST_TEST_TYPE=${params.tempest_test_type}
                  export AIRSHIP_TEMPEST_LOG_STDOUT=True
                  ./run.sh test
                """
            }
            post {
                success {
                    script {
                        try {
                            sh "./run.sh parse_tempest_ci"
                            junit testResults: 'tempest.xml', allowEmptyResults: true, keepLongStdio: true
                        } catch(e) {
                            echo "Could not parse tempest results"
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            script {
                sh 'ara generate html ara_report'
            }
            publishHTML target: [
                        allowMissing: false,
                        alwaysLinkToLastBuild: false,
                        keepAll: true,
                        reportDir: 'ara_report',
                        reportFiles: 'index.html',
                        reportName: 'ARA Report'
            ]
            logParser projectRulePath: 'log-parser-plugin-rules.txt', showGraphs: true, useProjectRule: true, parsingRulesPath: null
            script {
                try {
                    sh "source ${WORKSPACE}/sock_${env.SOCOK8S_ENVNAME} && ./run.sh gather_logs"
                    zip archive: true, dir: 'logs/', zipFile: 'logs.zip'
                    archiveArtifacts artifacts: 'logs.zip'
                } catch(e) {
                    echo "Could not gather logs"
                }
            }
            script {
                if (params.hold_instance_for_debug == true) {
                     try {
                        echo "You can reach this node by connecting to its floating IP as root user, with the default password of your image."
                        timeout(time: 3, unit: 'HOURS') {
                            input(message: "Waiting for input before deleting  env ${SOCOK8S_ENVNAME}.")
                        }
                    } catch(e) {
                        echo "Timed out waiting for input, proceeding"
                    }
                }
            }
        }
        cleanup {
            script {
                // Do not leave the ssh-agent running
                sh "pkill -f 'ssh-agent -a /tmp/${env.SOCOK8S_ENVNAME}'"
                if (fileExists("/tmp/${env.SOCOK8S_ENVNAME}.needcleanup")) {
                    try {
                        retry(3) {
                            sh './run.sh teardown'
                            sh 'rm /tmp/${SOCOK8S_ENVNAME}.needcleanup'
                        }
                    } catch (error) {
                        sh 'ansible-playbook playbooks/generic-notify_failure_rocket.yml -v'
                        // manually set the build to failure if we could not cleanup resources
                        currentBuild.result = 'FAILURE'
                    }
                }
                try {
                    zip archive: true, dir: 'dstat/', zipFile: 'dstat.zip'
                } catch(e) {
                    echo "Could not gather dstat logs"
                }
            }
        }
    }
}
